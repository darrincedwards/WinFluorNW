
' =================
' WinFluor XY Series
' =================
' XY Series recording synchronised with analogue channels 
' (c) J. Dempster, University of Strathclyde 2004
' V1.0 9/7/04
' V1.1 9/9/04 Settings now saved in INI file
'		Data file incrementing works better

sub Main

	' Create file system object
	Set FSO = CreateObject("Scripting.FileSystemObject")
	
	' Create WinFluor program object
	Set WINFLUOR = CreateObject("WINFLUOR.AUTO")

	'Create a new instance of the Bio-Rad Helper Class
	Set  Br = New BioRadHelper

	'Get the current live experiment
	Set LivePic = Br.LivePic
	'Retrieve the number of display planes from the current RawData
	NumPanes = Br.LivePic.PicMatDimensionDisp(0).IPicMatDimProxy.Elems

	' Default settings
	FileName = "c:\Experiments\XYDataFile.idr"
	ExperimentID = "" 
	NumFrames = 100
	PaneNum = 1


	' Load from INI file
	INIFileName = "c:\Experiments\winfluor XY Series.ini"
	if FSO.FileExists(INIFileName) then
	       	Set INIFile = FSO.OpenTextFile(INIFileName,1)
		FileName = INIFile.ReadLine
		ExperimentID = INIFile.ReadLine
		NumFrames = Cint(INIFile.ReadLine)
		PaneNum = Cint(INIFile.ReadLine)
		INIFile.Close
		Set INIFile = Nothing
	end if


	' Create new file name (with index number)
	WinFluor.NewFile FileName
	FileName = WinFluor.FileName

	'Get user input
	Dim mib
	Set mib = CreateObject("LS2KScriptingUI.MultipleInputBox")
	
	mib.numInputs = 4
	mib.InputCaption(1) = "WinFluor Filename"
	mib.InputText(1) = FileName

	mib.InputCaption(2) = "Experiment Info"
	mib.InputText(2) = ""

	mib.InputCaption(3) = "Number of frames"
	mib.InputMin(3) = 1
	mib.InputMax(3) = 32000
	mib.InputDecimalPlaces(3) = 0
	mib.InputText(3) = NumFrames 

	mib.InputCaption(4) = "Pane No. to be exported to WinFluor"
	mib.InputMin(4) = 1
	mib.InputMax(4) = NumPanes
	mib.InputDecimalPlaces(4) = 0
	mib.InputText(4) = PaneNum

	' Display box and wait until box is closed
	mib.Show
	Do until mib.closed
	Loop
 
	' Get parameters

	' Name of WinFluor data file
	if FileName <> mib.InputText(1) then
		' Re-create file
		FileName = mib.InputText(1)	
		WinFluor.NewFile FileName
	end if

	' Experiment ID
	WinFluor.ExperimentID = mib.InputText(2)

	' No. of frames to be acquired
	NumFrames = CInt(mib.InputText(3))
	RecordingTime = (NumFrames*Instrument.ScanBoxLines)/Instrument.ScanBaseSpeed

	'Pane # to save to WinFluor
	PaneNum = CInt(mib.InputText(4))

	' Set up XY Series
	ZStart = Instrument.ZMotorCurrentPosition
	ZStop = Instrument.ZMotorCurrentPosition
	ZStep = 1
	Instrument.ScanSetXYImages 0,ZStart,ZStop,ZStep,NumFrames,0
	
	' Start XY Series
	Instrument.ScanStart 3

	' Report status
	s = "XY Series Started: "
	s = s & Cint(NumFrames)
	s = s & " frames @" & CInt(Instrument.ScanBaseSpeed) & " lps"
	s = s & " (" & RecordingTime & " s )"
        StatusBar.SimpleText = s

	' Wait 500 ms
	Node.Sleep 500

	' Start WinFluor analogue recording
	WINFLUOR.RECORDLSM RecordingTime,NumFrames,2

	' Wait for completion of line scanning
	Instrument.WaitForScriptAcqToStop



	'Save XY Series image file to temp. file
	TempFileName = "e:\temp\winfluor temp.pic"
        StatusBar.SimpleText =  "Exporting XY Series images to " & TempFileName
	Call Br.SavePic(LivePic, TempFileName)

	if NumPanes > 1 then
		' Multiple panes .. select pane # to import
		Pos = InStr( 1, LCase(TempFileName), ".pic")
		if PaneNum = 1 then 
			TempFileName = Left(TempFileName,Pos-1) & "01.pic"
		elseif PaneNum = 2 then 
			TempFileName = Left(TempFileName,Pos-1) & "02.pic"
		elseif PaneNum = 3 then 
			TempFileName = Left(TempFileName,Pos-1) & "03.pic"
		else 
			TempFileName = Left(TempFileName,Pos-1) & "04.pic"
		end if
	end if

	' Import images from temp file into WinFluor
        StatusBar.SimpleText =  "Importing from " & TempFileName & " into WinFluor"
	WINFLUOR.ImportImageFile TempFileName

	' Save settings to INI file
	if FSO.FileExists(INIFileName) then 
          FSO.DeleteFile(INIFileName)'
	end if
        Set INIFile = FSO.CreateTextFile(INIFileName,TRUE)
	INIFile.WriteLine(WinFluor.FileName)
	INIFile.WriteLine(ExperimentID)
	INIFile.WriteLine(NumFrames)
	INIFile.WriteLine(PaneNum)
	INIFile.Close
	Set INIFile = Nothing
	
	
	' Disconnect from WinFluor
	Set WinFluor = Nothing

        StatusBar.SimpleText = "End of Script"


end sub











